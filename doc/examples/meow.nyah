grammar "meow"

skip WhitespaceNoNewlines = [ \t]+
Identifier = [_a-zA-Z]^[_a-zA-Z0-9]*

RawString = (
    '$' { fail("expected function declaration or variable assignment") } |
    . )*

StringNode = \" >> !\"* \" | \' >> !\'* \'

Yield = 'yield' >> '(' argument:Identifier ')'

SimpleNode = TextNode | HtmlNode | StringNode | RawString | Yield
Node = SimpleNode | HtmlNode->master | HtmlNode->important | VariableDefinition

HtmlArgumentValue = StringNode | [!,)]+

HtmlNodeArgument = (key name:Identifier '=')? value:HtmlArgumentValue

HtmlNode {
    children = '{' >> Node* '}'
    arguments = '(' >> (HtmlNodeArgument % ','?)? ')'
    nodeName = Identifier

    begin = nodeName children |
            ( '%' >> nodeName arguments? |
              nodeName arguments ) ^ (
            WhitespaceNoNewlines ^ (children as SimpleNode | children?))

    // alternate constructors
    begin master = '!' >> nodeName arguments? \n children as Node*
    begin important = '!!' >> nodeName arguments? \n ~
                      children as (Node - important - master)*
}

TextNode = '<<' ^ >> local textMarker:Identifier \n ~
            .*? \n ^ matched(textMarker)

MacroDefinition {
    arguments = '(' > (Identifier % ',')? ')'
    header = '$' 'function' >> name:Identifier arguments?

    children = Node*

    begin = '!' header children |
            header '{' children '}'
}

Expression = Number | StringNode

VariableDefinition = '$' name:Identifier '=' > expression:Expression

Global = (Node | MacroDefinition)*
