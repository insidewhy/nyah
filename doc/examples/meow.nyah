skip WhitespaceNoNewlines = [ \t]+

skip Whitespace = \s+
skip Comment = '//' !\n*
Identifier = [_a-zA-Z]^[_a-zA-Z0-9]*

RawString = (
    '$' { fail("expected function declaration or variable assignment") } |
    . )*

StringNode = \" create !\"* \" | \' create !\'* \'

Number = [0-9]+

SimpleNode = TextNode | HtmlNode | StringNode | RawString
Node = SimpleNode | VariableDefinition | MasterHtmlNode | Yield
HeadNode = Node | MacroDefinition

HtmlArgumentValue = StringNode | [!,)]+

HtmlNodeArgument = (name:Identifier '=')? value:HtmlArgumentValue

HtmlNode {
    children = '{' create Node* '}'
    arguments = '(' create (HtmlNodeArgument % ','?)? ')'
    nodeName = Identifier

    begin = nodeName children |
            ( '%' create nodeName arguments? |
              nodeName arguments create ) ~
            ( ^ WhitespaceNoNewlines ^ (children as SimpleNode | children?))

}

// ?: means this node will be identical to the HtmlNode type and also have the
//    same type identifier (it exists just to help the grammar out)
MasterHtmlNode ?: HtmlNode =
    '!' create nodeName arguments? ~ \n children:Node*

TextNode = '<<' ^ create local textMarker:Identifier \n ~
            .*? \n ^ matched(textMarker)

Yield = 'yield' create argument:Identifier

MacroDefinition {
    arguments = '(' create (Identifier % ',')? ')'
    header = '$function' create name:Identifier arguments?

    children = Node*

    begin = '!' header children |
            header '{' children '}'
}

Expression = Number | StringNode

// var Expression equivalent to expression:Expression
VariableDefinition = '$' name:Identifier '=' create var Expression

Global = HeadNode*
